sp {
  main = spaces rule rule* defaction*

  rule =
    | "^" spaces rulename "=" line? spaces pattern+ -- return_value
    | "#" spaces rulename "=" line? spaces pattern+ -- rewrite_value
    | spaces line spaces -- empty
    | comment -- comment

  defaction =
    | "#:" spaces rulename "=" spaces line? spaces action+ spaces -- internal
    | spaces line spaces -- empty
    | comment -- comment
    | kw<"_end"> -- end
    

  pattern =
    | pattern1 line? spaces -- pattern
    | line spaces -- empty
    
  pattern1 = stringMatch | endop | cond | peekcond | cycle | traceAccept | traceAll | trace | rulecall

  endop = kw<"_end"> line? spaces

  rulename = name spaces

  rulecall = name spaces

  stringMatch = string spaces
  
  string = dq notdq* dq
  dq = "\""
  notdq =
    | "\\" any -- escaped
    | ~dq any -- raw

  cond = "[" line? spaces condClause+ "]" line? spaces
  peekcond = "[*" line? spaces peekCondClause+ "]" line? spaces
  peekCondClause = "|" spaces peekCondMatch ":" spaces matchaction*
  condClause = "|" spaces condMatch ":" spaces matchaction*
  condMatch =
    | string -- string
    | endop -- endop
    | "*" -- else
  peekCondMatch =
    | string -- string
    | endop -- endop
    | "*" -- else
  matchaction = break | acceptAndAppend | pattern
  break = kw<"_break"> line? spaces
  acceptAndAppend = "." line? spaces
  
  cycle = "<<<" line? spaces pattern+ ">>>" line? spaces

  keyword = (
      kw<"_trace_all">
    | kw<"_trace">
    | kw<"_trace_accept">
    | kw<"_break">
    | kw<"_return_value">
    | kw<"_ignore_value">
    | kw<"_end">
    )

  kw<s> = 
    | "❲" s "❳" -- bracketed
    | s -- unbracketed

  name =
    | ~keyword "❲" firstLetter moreLetter* "❳" -- bracketedname
    | ~keyword firstLetter moreLetter* -- unbracketedname
  firstLetter = letter | "_"
  moreLetter = digit | firstLetter

  action =
    | kw<"_return_value"> spaces line? spaces -- return_value
    | kw<"_ignore_value"> spaces line? spaces -- ignore_value
    | "@" spaces string spaces line? spaces   -- shellout
    | trace_action -- trace

  trace_action =
    | traceAll  -- traceAll
    | traceAccept  -- traceAccept
    | trace  -- trace

  trace = kw<"_trace"> spaces string line? spaces
  traceAll = kw<"_trace_all"> spaces line? spaces
  traceAccept = kw<"_trace_accept"> spaces line? spaces

  line = "⎩" (~"⎩" ~"⎭" any)* "⎭"

  comment = "%" (~nl any)* nl
  nl = "\n"
  spaces += comment
}
